{
    meta: {
        language: "Calculator"
        description: "A simple expression evaluator language (for neos testing purposes)"
        language.file.extension: [ ".calc" ]
        copyright: "Copyright (C) 2019 Leigh Johnston"
        version: "1.0.0"
    }

    libraries: [
        neos.core
        neos.math
        neos.math.universal
    ]

    expect: statement

    character: {
        is: language.character.utf8
    }

    default: {
        ' ': whitespace
        character.LF: whitespace
        character.CR: whitespace
        character.TAB: whitespace
    }

    whitespace: {
        is: language.whitespace
        tokens: {
            ' ': whitespace
            character.LF: whitespace
            character.CR: whitespace
            character.TAB: whitespace
        }
    }

    statement: {
        is: language.statement
        expect: expression
        tokens: {
            expression: {
                expect: end
            }
            whitespace: ignore
        }
        end: {
            tokens: {
                ';': done
                whitespace: ignore
            }
        }
    }

    expression: {
        is: math.expression
        expect: term
        tokens: {
            term: {
                '+': math.operator.addition
                '-': math.operator.subtraction
                math.operator: {
                    expect: term
                    whitespace: ignore
                }
                whitespace: ignore
            }
        }
        term: {
            expect: primary
            tokens: {
                primary: {
                    '*': math.operator.multiplication
                    '/': math.operator.division
                    math.operator: {
                        expect: primary
                        whitespace: ignore
                    }
                    whitespace: ignore
                }
            }
        }
        primary: {
            is: math.expression.operand
            tokens: {
                character.digit: universal_number
                universal_number: done
                '-': math.operator.negate
                math.operator.negate: {
                    expect: primary
                    whitespace: ignore
                }
                '(': {
                    expect: expression
                    expression: {
                        expect: ')'
                        ')': done
                        whitespace: ignore
                    }
                    whitespace: ignore
                }
                whitespace: ignore
            }
        }
    }

    universal_number: {
        is: math.universal.number
        tokens: {
            character.digit: digit
            digit: {
                character.digit: digit
                '.': point
                point: {
                    character.digit: digit
                    'e': exponent
                    'E': exponent
                    whitespace: done
                }
                whitespace: done
            }
            '#': base
            base: {
                character.hexdigit: hexdigit
                '.': point
                point: {
                    character.hexdigit: hexdigit
                    '#': {
                        'e': exponent
                        'E': exponent
                        whitespace: done
                        character: next
                    }
                    whitespace: done
                }
                '#': {
                    'e': exponent
                    'E': exponent
                    whitespace: done
                }
            }
            exponent: {
                '+': exponent.positive
                exponent.positive: {
                    character.digit: exponent.digit
                    exponent.digit: {
                        character.digit: exponent.digit
                        whitespace: done
                    }
                }
                '-': exponent.negative
                exponent.negative: {
                    character.digit: exponent.digit
                    exponent.digit: {
                        character.digit: exponent.digit
                        whitespace: done
                    }
                }
            }
        }
    }
}